var Enumerable = (function() {
	function Sequence(start, end, step) {
		var ret = {};
		var val = start;
		
		for(var i = 0; val < end; val = val + step, i++)
		{
			ret[i] = val;
		}
		
		return ret;
	}
	
	function Range(start, count) {
		var ret = {};
		
		for(var i = 0; i < count; i++)
		{
			ret[i] = i + start;
		}
		return ret;
	}
	
	function Repeat(element, count) {
		var ret = {};
		for(var i = 0; i < count; i++)
		{
			ret[i] = element;
		}
		return ret;
	}
	
	function Generate(creator, count) {
		var ret = {};
		
		for(var i = 0; i < count; i++)
			ret[i] = creator(i);
		
		return ret;
	}
	
	function Where(filter) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var ret = {};
		var retIndex = 0;
		var index = 0;
		
		foreach(var e in this)
			if(filter(e, index++))
				ret[retIndex++] = e;
		
		return ret;
	}
	
	function Select(selector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var ret = {};
		var index = 0;
		
		foreach(var e in this)
		{
			ret[index] = selector(e, index);
			index++;
		}
		
		return ret;
	}
	
	function Reverse() extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		
		
		var ret = {};
		var index = length - 1;
		
		foreach(var e in this)
			ret[index--] = e;
		
		return ret;
	}
	
	function SequenceEqual(other) extension {
		if(this == null || other == null 
			|| (this.GetType() != "Table" && this.GetType() != "String") 
			|| (other.GetType() != "Table" && other.GetType() != "String"))
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		var olength = other.GetType() == "Table" ? other.Length : other.Length();
		
		if(length != olength) return false;
		
		var index = 0;
		foreach(var e in this)
			if(e != other[index++])
				return false;
		
		return true;
	}
	
	function ToDictionary(keySelector, valueSelector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var ret = {};
		var index = 0;
		foreach(var e in this)
		{
			ret[keySelector(e, index)] = valueSelector(e, index);
			index++;
		}
		
		return ret;
	}
	
	function ToListDictionary(keySelector, valueSelector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
			
		var ret = {};
		var index = 0;
		foreach(var e in this)
		{
			ret[index] = {Key = keySelector(e, index), Value = valueSelector(e, index)};
			index++;
		}
		
		return ret;
	}
	
	function Take(number) extension {
		var arg = this;
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		if(this.GetType() == "String") arg = String.ToTable(this);
		
		if(number > arg.Length)
			throw ArgumentOutOfRangeException("number > this.Length");
		
		var ret = {};
		Buffer.BlockCopy(arg, 0, ret, 0, number);
		return ret;
	}
	
	function TakeWhile(predicate) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var ret = {};
		var index = 0;
		foreach(var e in this)
		{
			if(!predicate(e, index)) break;
			ret[index] = e;
			index++;
		}
		
		return ret;
	}
	
	function Skip(number) extension {
		var arg = this;
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		if(this.GetType() == "String") arg = String.ToTable(this);
		
		if(number > arg.Length)
			throw ArgumentOutOfRangeException("number > this.Length");
		
		var ret = {};
		Array.Copy(arg, number, ret, 0);
		return ret;
	}
	
	function SkipWhile(predicate) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var ret = {};
		var retIndex = 0;
		var yielding = false;
		var index = 0;
		foreach(var e in this)
		{
			if(!yielding && !predicate(e, index++)) yielding = true;
			if(yielding) ret[retIndex++] = e;
		}
		
		return ret;
	}
	
	function ForEach(action) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var index = 0;
		foreach(var e in this)
		{
			action(e, index++);
		}
	}
	
	function First() extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
			
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		
		if(length < 1)
			throw ArgumentException();
		
		return this[0];
	}
	
	function FirstOrDefault() extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		
		if(length < 1) return null;
		
		return this[0];
	}
	
	function Last() extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String") || this.Length() < 1)
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		
		return this[length - 1];
	}
	
	function LastOrDefault() extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		
		if(length < 1) return null;
		
		return this[length - 1];
	}
	
	function Single() extension {
		return this.First();
	}
	
	function Single(what) extension {
		return this.Where(what).First();
	}
	
	function Any() extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		return length > 0;
	}
	
	function Any(what) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var index = 0;
		foreach(var e in this)
		{
			if(what(e, index++))
				return true;
		}
		return false;
	}
	
	function All(what) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var index = 0;
		foreach(var e in this)
			if(!what(e, index++))
				return false;
		return true;
	}
	
	function Concat(other) extension {
		if(this == null || other == null 
			|| (this.GetType() != "Table" && this.GetType() != "String") 
			|| (other.GetType() != "Table" && other.GetType() != "String"))
			throw ArgumentException();
		
		var ret = {};
		var index = 0;
		foreach(var e in this)
			ret[index++] = e;
		foreach(var e in other)
			ret[index++] = e;
		
		return ret;
	}
	
	function Zip(other, selector) extension {
		if(this == null || other == null 
			|| (this.GetType() != "Table" && this.GetType() != "String") 
			|| (other.GetType() != "Table" && other.GetType() != "String"))
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		var olength = other.GetType() == "Table" ? other.Length : other.Length();
		
		var ret = {};
		length = Math.Min(length, olength);
		
		for(i = 0; i < length; i++)
			ret[i] = selector(this[i], other[i]);
		
		return ret;
	}
	
	function Aggregate(func) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var length = this.GetType() == "Table" ? this.Length : this.Length();
		if(length < 1) return null;
		
		var result = this[0];
		foreach(var e in this)
			result = func(result, e);
		
		return result;
	}
	
	function Contains(what) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		return this.Any(function(e, i) return e == what);
	}
	
	function Min() extension {
		if(this == null || this.GetType() != "Table" || this.Length < 1)
			throw ArgumentException();
		
		var min = this[0];
		for(var i = 1; i < this.Length; i++)
		{
			if(this[i] < min)
				min = this[i];
		}
		
		return min;
	}
	
	function Min(selector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		return this.Select(selector).Min();
	}
	
	function Max() extension {
		if(this == null || this.GetType() != "Table" || this.Length < 1)
			throw ArgumentException();
		
		var max = this[0];
		for(var i = 1; i < this.Length; i++)
		{
			if(this[i] > max)
				max = this[i];
		}
		
		return max;
	}
	
	function Max(selector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		return this.Select(selector).Max();
	}
	
	function Average() extension {
		if(this == null || this.GetType() != "Table")
			throw ArgumentException();
		
		var sum = 0;
		var count = 0;
		for(var i = 0; i < this.Length; i++)
		{
			sum = sum + this[i];
			count++;
		}
		
		if(count > 0) return sum / count;
		throw ArgumentException();
	}
	
	function Average(selector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		return this.Select(selector).Average();
	}
	
	function Sum() extension {
		if(this == null || this.GetType() != "Table")
			throw ArgumentException();
		
		var sum = 0;
		
		for(var i = 0; i < this.Length; i++)
			sum = sum + this[i];
		
		return sum;
	}
	
	function Sum(selector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		return this.Select(selector).Sum();
	}
	
	function OrderBy(selector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		var keys = {};
		var ret = {};
		var index = 0;
		
		foreach(var e in this)
		{
			keys[index] = selector(e);
			ret[index] = e;
			index++;
		}
		
		Array.Sort(keys, ret);
		
		return ret;
	}
	
	function OrderByDescending(selector) extension {
		if(this == null || (this.GetType() != "Table" && this.GetType() != "String"))
			throw ArgumentException();
		
		return this.OrderBy(selector).Reverse();
	}
	
	return {
		Range = Range,
		Sequence = Sequence,
		Repeat = Repeat,
		Generate = Generate
	};
})();